# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Maintainer: Marius Gripsgard (marius@ubports.com)
# Maintainer: Furkan Kardame <furkan@fkardame.com>

pkgbase=lomiri
pkgname=(lomiri)
_pkgname=lomiri
pkgver=0.5.0
pkgrel=13
url='https://gitlab.com/ubports/development/core/lomiri'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
depends=(dbus-cpp biometryd xorg-xwayland lcov gcovr geonames lomiri-system-settings lomiri-api qmenumodel gnome-desktop lomiri-app-launch lomiri-ui-toolkit libqtdbustest libqtdbusmock lomiri-indicator-network libusermetrics liblightdm-qt5 lomiri-settings-components lomiri-schemas deviceinfo qtmir-git lomiri-telephony-service lomiri-notifications lomiri-thumbnailer hfd-service lomiri-download-manager)
makedepends=(git cmake cmake-extras doxygen)
checkdepends=(xorg-server-xvfb)
source=(
  'git+https://gitlab.com/ubports/development/core/lomiri.git#tag=0.5.0'
  'debian-patches.tar.gz::https://salsa.debian.org/ubports-team/lomiri/-/archive/debian/0.5.0-6/lomiri-debian-0.5.0-6.tar.gz'
  'force-CXX_20.patch'
  '0001-take-shells-rotation-into-account.patch'
  '0002-InputDispatchFilter-Wheel-take-Shell-s-rotation-into.patch'
)
sha256sums=('3e05440ce9bdd0d7f36e651ad00f059464198aba459aadabe51a1f427adc1779'
            '6bd0f3ad9a9db3a12cf3402a4c4487e932658c4ebe8754aceffe056151498cda'
            'c5dc8e491e059e88a9ce303c3266729c20c79955e80d56cdac759e9e79479265'
            '8bab2fbf7ba14af1ae90800cb2eeed15a63ee8a776f2eed64762045c53c74346'
            '775928071cffcdf069abbedef1b4f40b6d5ff40f6296ab7a8a84846cff580519')

BUILDENV+=('!check') # https://paste.ubuntu.com/p/2d33RJX4W8/

# pkgver() {
#   cd ${_pkgname}-${pkgver}
#   echo "r$(git rev-list --count HEAD).$(git describe --always)"
# }

prepare() {
  cd "${_pkgname}"

  # Apply Debian patches from the specified tag (debian/0.5.0-6)
  local patches_dir
  patches_dir=$(find "${srcdir}" -type d -path '*/debian/patches' -print -quit)
  if [[ -n "${patches_dir}" && -f "${patches_dir}/series" ]]; then
    while IFS= read -r p; do
      [[ -z "${p}" || "${p}" =~ ^# ]] && continue
      patch -Np1 -i "${patches_dir}/${p}"
    done < "${patches_dir}/series"
  fi
  # Apply local adjustments
  patch -Np1 -i "${srcdir}/0001-take-shells-rotation-into-account.patch"
  patch -Np1 -i "${srcdir}/0002-InputDispatchFilter-Wheel-take-Shell-s-rotation-into.patch"
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
  -DWITH_MIR2=ON \
  -DCMAKE_CXX_STANDARD=20 \
  -DCMAKE_CXX_STANDARD_REQUIRED=ON \
  -DCMAKE_CXX_EXTENSIONS=OFF \
  -DCMAKE_INSTALL_PREFIX:PATH=/usr \
  -DCMAKE_INSTALL_LIBDIR="lib/" \
  -DNO_TESTS=true ..
  make -j8
}

#check() {
#  cd ${_pkgname}/build
#  make ARGS+="--output-on-failure" test
#}

package() {
  pkgdesc='Lomiri shell'
  conflicts=(lomiri)
  provides=(lomiri)
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install

  # Move conf to the right directory
   #mv "${pkgdir}/usr/etc/dbus-1/system.d/com.canonical.Unity.conf"  "${pkgdir}/etc/dbus-1/system.d/"
}

# vim:set ts=2 sw=2 et:
