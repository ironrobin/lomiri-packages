# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=qtmir-git
pkgname=(qtmir-git)
_pkgname=qtmir
pkgver=r2907.0.7.2.r113.gea2f4774
pkgrel=8
url='https://gitlab.com/ubports/development/core/qtmir'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(qtmir)
provides=(qtmir)
depends=(qt5-base qt5-quickcontrols qt5-sensors mir process-cpp lomiri-app-launch gsettings-qt libqtdbustest libqtdbusmock lomiri-url-dispatcher glm valgrind)
makedepends=(boost git cmake cmake-extras gtest)
source=('git+https://gitlab.com/ubports/development/core/qtmir.git#commit=ea2f4774b1a109fec3e22a021fb21ff60da80626'
        'qtmir-debian-pkg.tar.gz::https://salsa.debian.org/ubports-team/qtmir/-/archive/debian/0.8.0_git20250407.ea2f477-6/qtmir-debian-0.8.0_git20250407.ea2f477-6.tar.gz'
        '3001_link_freetype_harfbuzz.patch'
        '3002_add-qt5mir2server.patch'
        '3003_take_shell_rotation_into_account.patch'
        '3004_add_touchpad_config.patch'
)
sha256sums=('f18fc49b66f4d43dcefd56961e7680435bcc910dece31fe996f5e679c6af24c0'
            '5fca70be7a579bfaa23331c72a8fe32d72e3dff00b94507360b83026c2120488'
            'f8ed4b35ec82c6b1f00186a5e2faf4399b50868241690a2fc20487e384f2c686'
            'f1077654c0d2e9201c234b7782f28ecb6385f19369952b6249195f2ac9ca98a5'
            '731633429641e75b8ed878568e346e674fdf33c55eb381db93149b55afbc8200'
            '4f7c0865289145d9d4b16f11b9dd0f972d386dda227c2d4d64ac1f4cd8f161cf')

BUILDENV+=('!check')

BUILD_DIR=build

prepare() {
  cd ${_pkgname}

  # Apply Debian patches from the provided packaging tarball
  local debian_dir
  debian_dir=$(ls -d ../qtmir-debian-*/debian 2>/dev/null | head -n1)
  if [[ -n "$debian_dir" && -f "$debian_dir/patches/series" ]]; then
    echo "Applying Debian patch series from: $debian_dir"
    while read -r p; do
      [[ -z "$p" || "$p" =~ ^# ]] && continue
      patch -Np1 -i "$debian_dir/patches/$p"
    done < "$debian_dir/patches/series"
  fi
  patch -Np1 -i "${srcdir}/3001_link_freetype_harfbuzz.patch"
  patch -Np1 -i "${srcdir}/3002_add-qt5mir2server.patch"
  patch -Np1 -i "${srcdir}/3003_take_shell_rotation_into_account.patch"
  patch -Np1 -i "${srcdir}/3004_add_touchpad_config.patch"
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DWITH_MIR2=ON \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DNO_TESTS=1 \
    -DCMAKE_INSTALL_LIBDIR="lib/" \
    -DBUILD_TESTING=OFF \
    -DCMAKE_CXX_STANDARD=20 \
    -DCMAKE_CXX_STANDARD_REQUIRED=ON \
    -DCMAKE_CXX_EXTENSIONS=ON \
    ..
  make -j8
}

# check() {
#   cd ${_pkgname}/${BUILD_DIR}
#   make ARGS+="--output-on-failure" test
# }

package_qtmir-git() {
  pkgdesc='Qt platform abstraction (QPA) plugin for a Mir server'
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
