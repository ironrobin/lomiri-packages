# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgbase=qtmir-git
pkgname=(qtmir-git)
_pkgname=qtmir
pkgver=r2920.0.7.2.r126.gca9e9a26
pkgrel=2
url='https://gitlab.com/ubports/development/core/qtmir'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(qtmir)
provides=(qtmir)
depends=(qt5-base qt5-quickcontrols qt5-sensors mir process-cpp lomiri-app-launch-git gsettings-qt libqtdbustest-git libqtdbusmock-git lomiri-url-dispatcher-git glm valgrind)
makedepends=(git cmake cmake-extras gtest)
source=('git+https://gitlab.com/ubports/development/core/qtmir.git#branch=personal/sunweaver/debian-upstream'
        '3001_link-freetype-harfbuzz-add-qt5mir2server.patch'
)
sha256sums=('SKIP'
            '8dfafa6d84ad48cd8f7d056e8e078e7565c9bd5abe1abdbea68c498a5888a51d'
)

BUILDENV+=('!check')

BUILD_DIR=build

prepare() {
  cd ${_pkgname}
  # TODO: See if this patch should be applied upstream
  patch -p1 < ../3001_link-freetype-harfbuzz-add-qt5mir2server.patch
}

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)" | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd ${_pkgname}
  mkdir -p ${BUILD_DIR}
  cd ${BUILD_DIR}
  cmake -DCMAKE_POLICY_VERSION_MINIMUM=3.5 \
    -DWITH_MIR2=ON \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DNO_TESTS=1 \
    -DCMAKE_INSTALL_LIBDIR="lib/" \
    -DBUILD_TESTING=OFF \
    -DCMAKE_CXX_STANDARD=20 \
    -DCMAKE_CXX_STANDARD_REQUIRED=ON \
    -DCMAKE_CXX_EXTENSIONS=ON \
    ..
  make
}

# check() {
#   cd ${_pkgname}/${BUILD_DIR}
#   make ARGS+="--output-on-failure" test
# }

package_qtmir-git() {
  pkgdesc='Qt platform abstraction (QPA) plugin for a Mir server'
  cd ${_pkgname}/${BUILD_DIR}
  make DESTDIR="${pkgdir}/" install
  mv ${pkgdir}/usr/lib/qt5/ ${pkgdir}/usr/lib/qt/
}
# vim:set ts=2 sw=2 et:
