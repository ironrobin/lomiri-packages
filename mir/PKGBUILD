# Maintainer: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Maintainer: Pranav <pranav.sharma.ama@gmail.com>
# Contributor: Neptune <neptune650@proton.me>
# Contributor: Bjoern Franke <bjo+aur<at>schafweide.org>
# Contributor: Ivan Semkin (ivan at semkin dot ru)
# Contributor: kikadf <kikadf.01@gmail.com>

pkgname=mir
pkgver=2.20.2
pkgrel=0
pkgdesc="Canonical's display server"
url="https://github.com/canonical/mir"
arch=(x86_64 i686 aarch64)
license=('GPL-2.0-or-later OR GPL-3.0-or-later')
depends=(boost-libs libglvnd lttng-ust libepoxy libxml++2.6 libinput yaml-cpp
         libxkbcommon  freetype2  hicolor-icon-theme libxcursor
         egl-wayland wayland
         glib2 glibc gcc-libs util-linux-libs libxcb libxkbcommon-x11 libdrm mesa libx11 gtest glibmm
         # explicit depends 
         bash systemd-libs libdisplay-info pixman
)
makedepends=(glm doxygen graphviz cmake ninja boost umockdev wlcs glmark2
             python-pillow python-dbus
             #gcovr lcov valgrind
             python-dbusmock
             glib2-devel
)
optdepends=('qterminal: required for miral demos'
            'ttf-ubuntu-font-family: required for miral demos'
            'qt5-wayland: required for miral demos'
            'xcursor-dmz: opt requirement for miral demos')
options=()
source=("https://github.com/canonical/mir/releases/download/v${pkgver}/mir-${pkgver}.tar.xz"
        "1003_cross.patch"
        "1004_fix-autopkgtest.patch"
        "2002_dont-dpkg-gensymbols-by-upstream.patch"
        "2003_ignore-package-abi-test.patch"
        "2004_python_to_python3.patch"
        "2005_skip-check-miroil-symbols.patch"
)
# 0xxx: Grabbed from upstream development.
# 1xxx: Possibly relevant for upstream adoption.
# 2xxx: Only relevant for official Debian release.
sha256sums=('c8994ba14d986d484daaec6c0ccb5f4ca7eb52746167899991a0983772c0fda8'
            'a779cead843cd71ca480fadf3511db52e8849b5f5093a2847ce204839e32f52f'
            'a50fc9a95642e3ada0d6a70e97bee9fdfa113bfcf5bd070f1680fe2e818b25cd'
            '0d905db592de5ef07d63f7c73df20b6d89e7e9526b51f01e3d325556fee0fc87'
            '9178bde76a91b2fd40e9623fbe2f2edd7b4c056ca7fa3a96d6cdffe0d95345e1'
            '630f38c48f877a8d1f33cd79f804883acf810e946ce4741ca30cb459011abc7d'
            '9c739b136e0ba8dddc85c07f613b5897ff81cae4f58da92398b9b12fbb6f2c34'
)

# glm doesnt ship with a glm.pc, but mir build falls back on cmake's find_package for non-debian systems

prepare() {
  cd "${pkgname}-${pkgver}"

  patch -p1 -i ../1003_cross.patch
  patch -p1 -i ../1004_fix-autopkgtest.patch
  patch -p1 -i ../2002_dont-dpkg-gensymbols-by-upstream.patch
  patch -p1 -i ../2003_ignore-package-abi-test.patch
  patch -p1 -i ../2004_python_to_python3.patch
  patch -p1 -i ../2005_skip-check-miroil-symbols.patch

}

build() {
  export CFLAGS+=" -ffat-lto-objects -O2"
  export CXXFLAGS+=" -ffat-lto-objects -O2"

  local _flags=(
    -B build 
    -S "${pkgname}-${pkgver}" 
    -GNinja
    -Wno-dev
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    # mir options
    -DMIR_FATAL_COMPILE_WARNINGS=OFF
    -DCMAKE_INSTALL_LIBEXECDIR=/usr/lib/$pkgname/
    -DMIR_BUILD_INTERPROCESS_TESTS=OFF
    -DMIR_RUN_WLCS_TESTS=OFF
    -DMIR_USE_PRECOMPILED_HEADERS=OFF
  )

  cmake "${_flags[@]}"
  cmake --build build
}

#check() {
#  ctest --test-dir build --output-on-failure
#}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
