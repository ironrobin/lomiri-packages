
pkgname=libaccounts-qt5
_pkgname=libaccounts-qt
pkgdesc='Qt-based client library for accessing the online accounts database'
pkgver=1.17
pkgrel=0
arch=(x86_64 aarch64)
url='https://gitlab.com/accounts-sso/libaccounts-qt'
license=(LGPL)
depends=(libaccounts-glib qt5-base)
makedepends=(doxygen)
conflicts=(libaccounts-qt)
provides=(libaccounts-qt)
replaces=(libaccounts-qt)
source=("git+https://gitlab.com/accounts-sso/libaccounts-qt.git#tag=VERSION_1.17")
sha256sums=('SKIP')

prepare() {
  cd "${_pkgname%-git}"
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro
}

build() {
  cd "${_pkgname%-git}"
  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib CONFIG+=release
  make
}

package() {
  cd "${_pkgname%-git}"
  make INSTALL_ROOT="$pkgdir" install

# Remove docs
  rm -r "$pkgdir"/usr/share
}
