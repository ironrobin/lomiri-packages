# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=lomiri-history-service-git
_pkgname=lomiri-history-service
pkgver=r1557.0.6_3_g411a293
pkgrel=0
pkgdesc='Service that provides call log and conversation history'
url='https://gitlab.com/ubports/development/core/lomiri-history-service'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL)
conflicts=(history-service)
provides=(history-service)
depends=(lcov gcovr libphonenumber telepathy-qt-git sqlite telepathy-mission-control qt5-pim-git)
makedepends=(git cmake)
checkdepends=(dbus-test-runner xorg-server-xvfb xorg-xauth)
source=('git+https://gitlab.com/ubports/development/core/lomiri-history-service.git#branch=main')
sha256sums=('SKIP')

# --   Package 'QtGLib-2.0', required by 'virtual:world', not found (qt-gstreamer)
# BUILDENV+=('!check') 
# Failing Tests: https://paste.ubuntu.com/p/hR9tBFXJ6Y/

pkgver() {
  cd ${_pkgname}
  ver="r$(git rev-list --count HEAD).$(git describe --always)"
  echo "${ver//-/_}"            # only: hyphens â†’ underscores
}

build() {
  cd ${_pkgname}
  cmake -DENABLE_WERROR=OFF -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" .
  make
}

# Skip due to failing tests
# check() {
#   cd ${_pkgname}
#   make ARGS+="--output-on-failure" test
# }

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
