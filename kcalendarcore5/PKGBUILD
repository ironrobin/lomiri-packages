# Maintainer: Antonio Rojas <arojas@archlinux.org>

_name=kcalendarcore
pkgname=${_name}5
pkgver=5.116.0
pkgrel=1
pkgdesc='The KDE calendar access library'
arch=(x86_64 aarch64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(libical qt5-base)
makedepends=(extra-cmake-modules doxygen qt5-tools)
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz)
sha256sums=('2f57ea993a720b1ce7ea4948350c8e3af9380736621d42ae42ae3b6eb539a53c')
# The key is expired, skipping gpg check
#validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
