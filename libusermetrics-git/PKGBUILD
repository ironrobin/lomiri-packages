# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=libusermetrics-git
_pkgname=libusermetrics
pkgver=683.d7cbc41
pkgrel=1
pkgdesc='Library for exporting anonymous metrics about users'
url='https://gitlab.com/ubports/development/core/libusermetrics'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3 LGPL GPL)
conflicts=(libusermetrics)
provides=(libusermetrics)
depends=(qt5-base qt5-xmlpatterns gsettings-qt apparmor click qdjango-git libqtdbustest)
makedepends=(git cmake cmake-extras doxygen intltool)
source=('git+https://gitlab.com/ubports/development/core/libusermetrics.git')
sha256sums=('SKIP')

BUILDENV+=('!check') # Some tests are broken

pkgver() {
  cd ${_pkgname}
  echo "$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_pkgname}
  # git checkout bionic
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DCMAKE_INSTALL_LIBEXECDIR="lib/" .
  make
}

# Tests working, but takes too long
# check() {
#   cd ${_pkgname}
#   make ARGS+="--output-on-failure" test
# }

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
